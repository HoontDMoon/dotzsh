#
# dotzsh : https://github.com/dotphiles/dotzsh
#
# A theme based on Steve Losh's prompt with VCS_INFO integration
# Tweaked with solarized colors for 256/16 color modes
#
# Authors:
#   Ben O'Hara <bohara@gmail.com>
#

prompt_dotphiles_help () {
  cat <<'EOH'

General Look

  user at host in ~
   %

Options

  You can change the colours of the theme.

  % prompt dotphiles [<theme> [<colour1> ]

                 theme   : dark or light
                 colour1 : [user] at [host]

  The defaults are dark and green

Example

  prompt dotphiles dark red
  prompt dotphiles light orange

Colors

   yellow orange red magenta violet blue cyan green

EOH
}

function prompt_dotphiles_precmd {
  if [[ -n "$__PROMPT_dotphiles_VCS_UPDATE" ]] ; then
    # Check for untracked files or updated submodules since vcs_info doesn't.
    if [[ ! -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
      __PROMPT_dotphiles_VCS_UPDATE=1
      fmt_branch="$branchopen%s:$prompt_orange%b%f%u%c$prompt_yellow$dirty%f$branchclose"
    else
      fmt_branch="$branchopen%s:$prompt_orange%b%f%u%c%f${branchclose}"
    fi
    zstyle ':vcs_info:*:prompt:*' formats "${fmt_branch}"

    vcs_info 'prompt'
    __PROMPT_dotphiles_VCS_UPDATE=''
  fi
}

function prompt_dotphiles_preexec {
  case "$(history $HISTCMD)" in
    (*git*)
      __PROMPT_dotphiles_VCS_UPDATE=1
    ;;
    (*svn*)
      __PROMPT_dotphiles_VCS_UPDATE=1
    ;;
  esac
}

function prompt_dotphiles_chpwd {
  __PROMPT_dotphiles_VCS_UPDATE=1
}

function prompt_dotphiles_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Check for the minimum supported version.
  min_zsh_version='4.3.10'
  if autoload -Uz is-at-least && is-at-least "$min_zsh_version"; then
    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info
    add-zsh-hook precmd prompt_dotphiles_precmd
    add-zsh-hook preexec prompt_dotphiles_preexec
    add-zsh-hook chpwd prompt_dotphiles_chpwd
    __PROMPT_dotphiles_VCS_UPDATE=1

    RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %{${prompt_red}%}[$?]%f)${VIM:+" %B%{${prompt_green}%}V%f%b"}'

    if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
      prompt_base03="%F{234}"  # brblack base03
      prompt_base02="%F{235}"  # black base02
      prompt_base01="%F{240}"  # brgreen base01
      prompt_base00="%F{241}"  # bryellow base00
      prompt_base0="%F{244}"  # brblue base0
      prompt_base1="%F{245}"  # brcyan base1
      prompt_base2="%F{254}" # white base2
      prompt_base3="%F{230}" # brwhite base3
      prompt_yellow="%F{136}" # yellow yellow
      prompt_orange="%F{166}" # brred orange
      prompt_red="%F{160}" # red red
      prompt_magenta="%F{125}" # magenta magenta
      prompt_violet="%F{61}" # brmagenta violet
      prompt_blue="%F{33}" # blue blue
      prompt_cyan="%F{37}" # cyan cyan
      prompt_green="%F{64}" # green green
    else
      prompt_base03="%B%F{black}"  # brblack base03
      prompt_base02="%b%F{black}"  # black base02
      prompt_base01="%B%F{green}"  # brgreen base01
      prompt_base00="%B%F{yellow}"  # bryellow base00
      prompt_base0="%B%F{blue}"  # brblue base0
      prompt_base1="%B%F{cyan}"  # brcyan base1
      prompt_base2="%b%F{white}" # white base2
      prompt_base3="%B%F{white}" # brwhite base3
      prompt_yellow="%b%F{yellow}" # yellow yellow
      prompt_orange="%B%F{red}" # brred orange
      prompt_red="%b%F{red}" # red red
      prompt_magenta="%b%F{magenta}" # magenta magenta
      prompt_violet="%B%F{magenta}" # brmagenta violet
      prompt_blue="%b%F{blue}" # blue blue
      prompt_cyan="%b%F{cyan}" # cyan cyan
      prompt_green="%b%F{green}" # green green
    fi
  else
    prompt_base03="$fg_bold[black]"  # brblack base03
    prompt_base02="$fg[black]"  # black base02
    prompt_base01="$fg_bold[green]"  # brgreen base01
    prompt_base00="$fg_bold[yellow]"  # bryellow base00
    prompt_base0="$fg_bold[blue]"  # brblue base0
    prompt_base1="$fg_bold[cyan]"  # brcyan base1
    prompt_base2="$fg[white]" # white base2
    prompt_base3="$fg_bold[white]" # brwhite base3
    prompt_yellow="$fg[yellow]" # yellow yellow
    prompt_orange="$fg_bold[red]" # brred orange
    prompt_red="$fg[red]" # red red
    prompt_magenta="$fg[magenta]" # magenta magenta
    prompt_violet="$fg_bold[magenta]" # brmagenta violet
    prompt_blue="%fg[blue]" # blue blue
    prompt_cyan="$fg[cyan]" # cyan cyan
    prompt_green="$fg[green]" # green green
    RPROMPT=''
  fi
  unset min_zsh_version

  userprompt=">>"
  branchopen="("
  branchclose=")"
  unstaged="x"
  staged="+"
  dirty="*"

  # Enable VCS systems you use.
  zstyle ':vcs_info:*' enable git hg svn cvs

  # check-for-changes can be really slow.
  # You should disable it if you work with large repositories.
  zstyle ':vcs_info:*:prompt:*' check-for-changes true

  # Formats:
  # %b - branchname
  # %u - unstagedstr (see below)
  # %c - stagedstr (see below)
  # %a - action (e.g. rebase-i)
  # %R - repository path
  # %S - path in the repository
  local fmt_branch="${branchopen}%s:$prompt_orange%b%f%u%c%f${branchclose}"
  local fmt_action="($prompt_yellow%a%f)"
  local fmt_unstaged="$prompt_cyan${unstaged}%f"
  local fmt_staged="$prompt_green${staged}%f"

  zstyle ':vcs_info:*:prompt:*' unstagedstr   "${fmt_unstaged}"
  zstyle ':vcs_info:*:prompt:*' stagedstr     "${fmt_staged}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch} ${fmt_action}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  prompt_theme=${1:-'dark'}
  _prompt_host=${2:-'green'}

  zstyle ':dotzsh:module:editor:keymap:primary' overwrite " ${prompt_red}♺%f"
  zstyle ':dotzsh:module:editor:keymap' alternate " ${prompt_yellow}❮%f%B${prompt_red}❮%f%b${prompt_red}❮%f"

  SPROMPT='zsh: correct ${prompt_red}%R%f$reset_color to ${prompt_green}%r%f$reset_color [nyae]? '

  eval prompt_host="$"prompt_$_prompt_host""
  if [ "prompt_theme" = 'light' ]; then
  zstyle ':dotzsh:module:editor' completing '%{$prompt_base1%}...%f$reset_color'
    PROMPT='%{%(!.$prompt_red.$prompt_host)%}%n%f%{$prompt_base01%} at %{$prompt_host%}%m%f%{$prompt_base01%} in %{$prompt_base1%}%~ %f${vcs_info_msg_0_}
%{$prompt_base01%} ${userprompt} %{$prompt_base00%}'
  else
    zstyle ':dotzsh:module:editor' completing '%{$prompt_base01%}...%f$reset_color'
    PROMPT='%(!.%U%{$prompt_red%}.%{$prompt_host%})%n%u%f%{$prompt_base1%} at %{$reset_color%}%{$prompt_host%}%m%f%{$prompt_base1%} in %{$reset_color%}%{$prompt_base01%}%~ %f${vcs_info_msg_0_}
%{$prompt_base1%} ${userprompt} %{$reset_color%}%{$prompt_base0%}'
  fi
}

prompt_dotphiles_setup "$@"
